plugins {
    id 'java'
    id 'signing'
    id 'maven-publish'
    id 'com.github.hauner.jarTest'
    id 'jacoco'
}

jar {
    archivesBaseName = 'nitrite-rocksdb-adapter'
}

java {
    withJavadocJar()
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(configuration: 'default', path: ':nitrite')
    implementation "org.slf4j:slf4j-api:1.7.30"
    implementation "org.rocksdb:rocksdbjni:6.11.4"
    implementation "com.esotericsoftware:kryo:4.0.2"

    annotationProcessor "org.projectlombok:lombok:1.18.12"

    testAnnotationProcessor "org.projectlombok:lombok:1.18.12"
    testImplementation "uk.co.jemos.podam:podam:7.2.4.RELEASE"
    testImplementation "com.github.javafaker:javafaker:1.0.2"
    testImplementation "junit:junit:4.13"
    testImplementation "org.apache.lucene:lucene-core:8.6.1"
    testImplementation "org.apache.lucene:lucene-analyzers-common:8.6.2"
    testImplementation "org.apache.lucene:lucene-queryparser:8.6.1"
    testImplementation "org.apache.logging.log4j:log4j-slf4j-impl:2.13.3"
    testImplementation "org.apache.logging.log4j:log4j-core:2.13.3"
    testImplementation "org.awaitility:awaitility:4.0.3"
    testImplementation "joda-time:joda-time:2.10.6"
    testImplementation "org.meanbean:meanbean:2.0.3"
    testImplementation "com.fasterxml.jackson.core:jackson-databind:2.11.2"
    testImplementation "commons-io:commons-io:2.7"
    testImplementation "com.google.guava:guava:29.0-jre"
}

test {
    testLogging.showStandardStreams = false
    testLogging.exceptionFormat = 'full'
}

jacocoTestReport {
    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        csv.enabled false
        html.destination file("${buildDir}/reports/coverage")
    }
}

check.dependsOn jacocoTestReport

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'nitrite-rocksdb-adapter'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Nitrite RocksDB Storage Engine'
                description = 'RocksDB powered storage engine for Nitrite.'
                url = 'https://github.com/nitrite/nitrite-java'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'anidotnet'
                        name = 'Anindya Chatterjee'
                        email = 'anidotnet@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:nitrite/nitrite-java.git'
                    developerConnection = 'scm:git:git@github.com:nitrite/nitrite-java.git'
                    url = 'git@github.com:nitrite/nitrite-java.git'
                }
            }
        }
    }
    repositories {
        if (version.endsWith('SNAPSHOT') || project.hasProperty('release')) {
            maven {
                name = "OSSRH"
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username System.getenv('MAVEN_USERNAME')
                    password System.getenv('MAVEN_PASSWORD')
                }

            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}


javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}